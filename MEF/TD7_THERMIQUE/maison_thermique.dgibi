*        1         2         3         4         5         6         7
*23456789012345678901234567890123456789012345678901234567890123456789012
************************************************
TITR 'MAISON' ;
OPTI DIME 2 ELEM QUA8;
*OPTI ECHO 0;

************************************************
* parametrisation, donnees
************************************************
H        = 2.5 ;
L        = 4.0 ;
EPMUR    = 25e-2;
EPISOL   = 8e-2;
LTER     = 5.0;
ERADIA   = 70e-2;

NE       = 6 ; 
NISOL    = ENTI(NE/EPMUR*EPISOL) ;
NL       = ENTI(NE/EPMUR*L);
NH       = ENTI(NE/EPMUR*H);
NTER     = ENTI(NE/EPMUR*LTER);
NRADIA   = ENTI(NE/EPMUR*ERADIA);

TEXT    = 6.0 ;
PUISINT = 800 ;

LAMBISO = 0.02;
LAMBMUR = 1.7; 
LAMBRAD = 250;
LAMBAIR = 0.15;

************************************************
* geometrie et maillage
************************************************

P0 = 0 0;  P1 = 0 H;  P2 = (L/2) (1.5*H);  P3 = L H; P4 =L 0;
P0I = EPISOL 0; P1I = EPISOL H; P2I = (L/2) (1.5*H-EPISOL);
P3I = (L-EPISOL) H; P4I =(L-EPISOL) 0;

L1 = P0 DROI NH P1 ; L2 = P1 DROI (ENTI(NL/2)) P2 ;
L3 = P2 DROI (ENTI(NL/2)) P3 ; L4 = P3 DROI NH P4 ;
L5 = P4 DROI NL P0 ;
CMURINT = L1 ET L2 ET L3 ET L4 ET L5;

L6 = P4 DROI NISOL P4I ;
L7 = P4I D NH P3I; L8 = P3I D (ENTI(NL/2)) P2I ;
L9 = P2I D (ENTI(NL/2)) P1I; L10 = P1I D NH P0I; 
L11= P0I D NISOL P0;
CISOL = L1 ET L2 ET L3 ET L4 ET L6 ET L7 ET L8 ET L9 ET L10 ET L11;
SISOL = SURF CISOL PLAN ;

CMUREXT = CMURINT HOMO (1+(EPMUR/(L/2))) ((L/2) (0.9*H/2)) COUL VERT;
SMUR = SURF (CMURINT ET (INVE CMUREXT)) COUL ROUG ;

DISRAD = ERADIA/6;

PR1 = P0I PLUS (DISRAD 0); PR2 = PR1 PLUS (0 ERADIA);
PR3 = PR2 PLUS (ERADIA 0); PR4 = PR1 PLUS (ERADIA 0);
LR1 = PR1 D NRADIA PR2; LR2 = PR2 D NRADIA PR3;  
LR3 = PR3 D NRADIA PR4; LR4 = PR4 D NRADIA PR1; 
SRADIA = SURF (LR1 ET LR2 ET LR3 ET LR4) PLAN; 

LA1 = P4I D (ENTI(NL/L*(L-DISRAD-ERADIA))) PR4 ;
LA2 = PR1 D (ENTI(NL/L*(DISRAD))) P0I;
SAIR = SURF ((INVE LA2) ET LR1 ET LR2 ET LR3 ET (INVE LA1) ET 
             L7 ET L8 ET L9 ET L10) COUL BLEU;

STOT = SMUR ET SISOL ET SRADIA ET SAIR;
ELIM STOT (L/NL/5);
TRAC QUAL STOT; 

***************************************************
MODMUR = MODL SMUR THERMIQUE ISOTROPE ;
MATMUR = MATE MODMUR 'K' LAMBMUR;
CONMUR = COND MODMUR MATMUR;

MODISOL = MODL SISOL THERMIQUE ISOTROPE ;
MATISOL = MATE MODISOL 'K' LAMBISO;
CONISOL = COND MODISOL MATISOL;

MODRADIA = MODL SRADIA THERMIQUE ISOTROPE ;
MATRADIA = MATE MODRADIA 'K' LAMBRAD;
CONRADIA = COND MODRADIA MATRADIA;

MODAIR = MODL SAIR THERMIQUE ISOTROPE ;
MATAIR = MATE MODAIR 'K' LAMBAIR;
CONAIR = COND MODAIR MATAIR;

MOD1 = MODMUR ET MODISOL ET MODRADIA ET MODAIR;
MAT1 = MATMUR ET MATISOL ET MATRADIA ET MATAIR;
COND1= CONMUR ET CONISOL ET CONRADIA ET CONAIR;

*****************************************************
CL = BLOQ T CMUREXT;
CONDT = COND1 ET CL ;
TIMP = DEPI CL TEXT; 

Q = SOURCE MOD1 SRADIA PUISINT ;
CHART = Q ET TIMP;

TSOL = RESO CONDT CHART;

TRAC TSOL STOT;

FIN;

