*        1         2         3         4         5         6         7
*23456789012345678901234567890123456789012345678901234567890123456789012
************************************************
TITR 'DILATATION BICOUCHE' ;
OPTI DIME 2 ELEM QUA8 MODE PLAN DEFO;
*OPTI ECHO 0;

************************************************
* parametrisation, donnees
************************************************
LARG        = 0.10 ;
HAUT        = 0.02 ;
ESPA        = LARG/5;

NHAUT    = 10 ; 
NLARG    = ENTI(NHAUT/HAUT*LARG) ;
NESPA    = ENTI(NHAUT/HAUT*ESPA);

TSUP    = 30 ;
TINF    = 70 ;

LAMBINF = 70E-6; YINF = 200E9; NUINF = 0.4;
LAMBSUP = 250E-6;  YSUP = 100E9; NUSUP = 0.25;
LAMBAIR = 0.15E-6; YAIR = 1.; NUAIR = 0.;

************************************************
* geometrie et maillage
************************************************

P1 = 0 0;  P2 = ESPA 0;  P3 = LARG 0;  P4 = LARG HAUT;
P6 = P1 PLUS (0 HAUT);
P5 = P2 PLUS (0 HAUT);
P4 = P3 PLUS (0 HAUT);
P7 = P4 PLUS (0 HAUT);
P8 = P5 PLUS (0 HAUT);

L1 = P1 D NESPA P2 ; L2 = P2 D NHAUT P5 ;
L3 = P5 D NESPA P6 ; L4 = P6 D NHAUT P1 ;
SINF1= DALL L1 L2 L3 L4 COUL ROUG;
L5 = P3 D NLARG P2 ; L6 = P2 D NHAUT P5 ;
L7 = P5 D NLARG P4 ; L8 = P4 D NHAUT P3 ;
SINF2= DALL L5 L6 L7 L8 COUL ROUG;
SINF=SINF1 ET SINF2;
SAIR = SINF1 PLUS (0 HAUT) COUL BLAN;
SSUP2 = SINF2 PLUS (0 HAUT) COUL VERT;
SSUP = SAIR ET SSUP2;
STOT= SINF ET SSUP;
ELIM (HAUT/NHAUT/10) STOT;

TRAC QUAL STOT; 

***************************************************
MODINF = MODL SINF MECANIQUE ELASTIQUE ISOTROPE ;
MATINF = MATE MODINF YOUN YINF NU NUINF ALPH LAMBINF;

MODSUP = MODL SSUP MECANIQUE ELASTIQUE ISOTROPE ;
MATSUP = MATE MODSUP YOUN YSUP NU NUSUP ALPH LAMBSUP;

MODAIR = MODL SAIR MECANIQUE ELASTIQUE ISOTROPE ;
MATAIR = MATE MODAIR YOUN YAIR NU NUAIR ALPH LAMBAIR;

MODTOT = MODINF ET MODSUP ET MODAIR;
MATTOT = MATINF ET MATSUP ET MATAIR;

*****************************************************
LINF = STOT POIN DROI P1 P2 1E-8;
LSUP = STOT POIN DROI (0 (2*HAUT)) (1 (2*HAUT)) 1E-8;
LGAU = STOT POIN DROI P1 P6 1E-8;
CL1 = BLOQ UY LINF;
CL2 = BLOQ UX LGAU ;
CLM = CL1 ET CL2;

CHPOTEM1 = MANU CHPO LINF 1 'T' TINF;
SIGM1 = THET MODTOT MATTOT CHPOTEM1;
CHAR1 = BSIG MODTOT SIGM1 MATTOT;
CHPOTEM2 = MANU CHPO LSUP 1 'T' TSUP;
SIGM2 = THET MODTOT MATTOT CHPOTEM2;
CHAR2 = BSIG MODTOT SIGM2 MATTOT;
CHART = CHAR1 ET CHAR2;

*****************************************************
RIGT = RIGI MODTOT MATTOT;
RIGT = RIGT ET CLM;

DEPL1 = RESO RIGT CHART;

DEF0 = DEFO STOT DEPL1 0. BLAN;
DEF1 = DEFO STOT DEPL1 BLEU;
TRAC (DEF0 ET DEF1) CACH;

SS   = SIGM MODTOT MATTOT DEPL1;
SSVM = VMIS MODTOT SS;
TRAC DEF1 MODTOT SSVM;

SSXY = EXCO SMXY SS;
TRAC DEF1 MODTOT SSXY;
SSYY = EXCO SMYY SS;
TRAC DEF1 MODTOT SSYY;
SSXX = EXCO SMXX SS;
TRAC DEF1 MODTOT SSXX;



FIN;

